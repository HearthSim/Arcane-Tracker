plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id "net.mbonnin.android.strings2xls" version "0.1.4"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

static def extraFile(filename) {
    "../extra/app/" + filename
}

android {
    compileSdkVersion atConfig.compileSdkVersion
    buildToolsVersion atConfig.buildToolsVersion

    lintOptions {
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "net.mbonnin.arcanetracker"
        minSdkVersion 19
        targetSdkVersion atConfig.targetSdkVersion
        versionCode 234
        versionName "2.34"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }

    signingConfigs {
        mbonnin {
            keyAlias 'mbonnin'
            keyPassword 'password'
            storeFile file(extraFile('keystore.jks'))
            storePassword 'password'
            // sha1: 46:AF:3F:32:C0:F9:9C:EA:29:4E:F0:73:4D:2F:73:0B:E4:40:BB:7C
        }
    }

    dataBinding {
        enabled = true
    }

    def props = new Properties()
    def f = project.file(extraFile('keystore.properties'))
    if (f.exists()) {
        props.load(new FileInputStream(f))

        signingConfigs.mbonnin.storeFile project.file(props.keyStore)
        signingConfigs.mbonnin.storePassword props.keyStorePassword
        signingConfigs.mbonnin.keyAlias props.keyAlias
        signingConfigs.mbonnin.keyPassword props.keyAliasPassword
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(f.exists()) {
                signingConfig signingConfigs.mbonnin
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(f.exists()) {
                signingConfig signingConfigs.mbonnin
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/java'
    }
}

repositories {
    jcenter()
    google()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation dep.support_v4
    implementation dep.appcompat_v7
    implementation dep.design
    implementation dep.recyclerview_v7
    implementation dep.cardview_v7

    implementation "com.android.support.constraint:constraint-layout:1.0.2"

    implementation dep.gson
    implementation 'io.paperdb:paperdb:2.6'
    implementation dep.okhttp
    implementation dep.kotlin_stdlib
    implementation dep.kotlin_reflect

    implementation dep.timber
    implementation 'com.annimon:stream:1.1.9'

    implementation "io.reactivex.rxjava2:rxjava:2.1.6"
    implementation 'io.reactivex:rxandroid:1.2.1'
    implementation 'io.reactivex:rxkotlin:1.0.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'

    implementation dep.play_auth
    implementation dep.play_firebase_core
    implementation dep.play_firebase_crash
    implementation dep.play_firebase_messaging

    implementation 'com.android.support:multidex:1.0.2'
    implementation 'com.jakewharton.picasso:picasso2-okhttp3-downloader:1.1.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.android.support:multidex:1.0.2'
    implementation project(":detector")
    implementation dep.ktx

    testImplementation dep.junit
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test:rules:1.0.1"
    androidTestImplementation "com.android.support:multidex:1.0.2"
    kapt "com.android.databinding:compiler:$android_gradle_plugin_version"
}

def googleServicesFile = file('google-services.json')
def googleServicesExtraFile = file(extraFile('google-services.json'))
def googleServicesMockFile = file('google-services.json.mock')

if (!googleServicesFile.exists()){
    if (googleServicesExtraFile.exists()) {
        println 'using extra google-services.json'
        googleServicesFile.bytes = googleServicesExtraFile.bytes
    } else {
        println 'using mock google-services.json'
        googleServicesFile.bytes = googleServicesMockFile.bytes
    }
}

apply plugin: 'com.google.gms.google-services'
