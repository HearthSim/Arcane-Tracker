plugins {
    id 'com.github.ben-manes.versions' version '0.17.0'
    id "net.mbonnin.android.strings2xls" version "0.1.4"
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

static def extraFile(filename) {
    "../extra/app/" + filename
}

android {
    compileSdkVersion Versions.compileSdkVersion

    lintOptions {
        disable 'MissingTranslation'
    }

    defaultConfig {
        applicationId "net.mbonnin.arcanetracker"
        minSdkVersion 19
        targetSdkVersion Versions.targetSdkVersion
        versionCode 304
        versionName "3.04"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation":
                                     "$projectDir/schemas".toString()]
            }
        }
    }

    signingConfigs {
        mbonnin {
            keyAlias 'mbonnin'
            keyPassword 'password'
            storeFile file(extraFile('keystore.jks'))
            storePassword 'password'
            // sha1: 46:AF:3F:32:C0:F9:9C:EA:29:4E:F0:73:4D:2F:73:0B:E4:40:BB:7C
        }
    }

    dataBinding {
        enabled = true
    }

    def props = new Properties()
    def f = project.file(extraFile('keystore.properties'))
    if (f.exists()) {
        props.load(new FileInputStream(f))

        signingConfigs.mbonnin.storeFile project.file(props.keyStore)
        signingConfigs.mbonnin.storePassword props.keyStorePassword
        signingConfigs.mbonnin.keyAlias props.keyAlias
        signingConfigs.mbonnin.keyPassword props.keyAliasPassword
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(f.exists()) {
                signingConfig signingConfigs.mbonnin
            }
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if(f.exists()) {
                signingConfig signingConfigs.mbonnin
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/INDEX.LIST'
    }

    sourceSets {
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/java'
    }
    buildToolsVersion '27.0.3'
}

repositories {
    jcenter()
    google()
}

androidExtensions {
    experimental = true
}

dependencies {

    implementation Libs.support_v4
    implementation Libs.appcompat_v7
    implementation Libs.design
    implementation Libs.recyclerview_v7
    implementation Libs.cardview_v7

    implementation Libs.constraintLayout

    implementation Libs.gson
    implementation Libs.paperDb
    implementation Libs.okhttp
    implementation Libs.kotlin_stdlib
    implementation Libs.kotlin_reflect

    implementation Libs.timber
    implementation Libs.annimonStream

    implementation Libs.rxjava2
    implementation Libs.rxAndroid
    implementation Libs.rxAndroid2
    implementation Libs.rxKotlin
    implementation Libs.retrofit
    implementation Libs.gson
    implementation Libs.retrofitRx
    implementation Libs.retrofitRx2

    implementation Libs.play_auth
    implementation Libs.play_firebase_core
    implementation Libs.play_firebase_messaging

    implementation Libs.multidex
    implementation Libs.picassoDownloader
    implementation Libs.picasso
    implementation Libs.retrofitGson
    implementation Libs.ktx
    implementation Libs.crashlytics
    implementation Libs.room
    implementation Libs.rxRoom
    implementation Libs.stetho
    implementation Libs.stethoOkhttp
    implementation "android.arch.paging:runtime:1.0.0-rc1"

    kapt Libs.roomProcessor

    implementation project(":detector")

    testImplementation Libs.junit
    androidTestImplementation Libs.test_runner
    androidTestImplementation Libs.test_rules
    androidTestImplementation Libs.multidex
    androidTestImplementation(Libs.espresso, {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
}

def googleServicesFile = file('google-services.json')
def googleServicesExtraFile = file(extraFile('google-services.json'))
def googleServicesMockFile = file('google-services.json.mock')

if (!googleServicesFile.exists()){
    if (googleServicesExtraFile.exists()) {
        println 'using extra google-services.json'
        googleServicesFile.bytes = googleServicesExtraFile.bytes
    } else {
        println 'using mock google-services.json'
        googleServicesFile.bytes = googleServicesMockFile.bytes
    }
}

apply plugin: 'com.google.gms.google-services'
